(*
Manipulation
============

The Manipulation file is for any procedures and/or functions that change
something or converts it to something else. Something like converting a string
array to an integer array.

The source for can be found
`here <https://github.com/SRL/MSI/raw/master/MSI/Core/Manipulation.simba>`_.

*)

(*
MSI_ClearFile
~~~~~~~~~~~~~

.. code-block:: pascal

  procedure MSI_ClearFile(FilePath: string);

Deletes all text in file 'FilePath', but leaves the file itself. Used usually
for clearing a file to avoid confusion before rewriting to it.

.. note::

  | Author: Coh3n
  | Last Updated: 11 January 2011 by Coh3n

Example:

.. code-block:: pascal

  MSI_ClearFile(PATH_PLAYER); // Clears the Player.txt file used to save form input
*)
procedure MSI_ClearFile(FilePath: string);
var
  f: Integer;
begin
  try
    f := RewriteFile(FilePath, False);
    CloseFile(f);
  except
    MSI_Debug('Error clearing file: ' + FilePath + ' - ' + ExceptionToString(ExceptionType, ExceptionParam));
  end;
end;

(*
MSI_ResetRemote
~~~~~~~~~~~~~~~

.. code-block:: pascal

  procedure MSI_ResetRemote;

Checks to see if the Player Remote file was updated. If it was, it sets the new
player information and resets the file.

.. note::

  | Author: Coh3n
  | Last Updated: 07 September 2011 by Coh3n

Example:

.. code-block:: pascal

  procedure MSI_ResetRemote;
*)
procedure MSI_ResetRemote;
var
  i: Integer;
begin
  case MSI_PointerCall(MSI_OnResetRemoteCall, []) of
    EXTRA_CONTINUE: ;
    EXTRA_EXIT: Exit;
  end;

  try
    {$IFDEF SRL_REMOTE}
    SRLRemote;
    {$ENDIF}

    SetLength(MSI_Players, length(players));

    for i := 0 to High(MSI_Players) do
    begin
      MSI_Players[i].Name := Players[i].Name;
      MSI_Players[i].Pass := Players[i].Pass;
      MSI_Players[i].Nick := Players[i].Nick;
      MSI_Players[i].Pin := Players[i].Pin;
      MSI_Players[i].Active := Players[i].Active;
      MSI_Players[i].TotalLoads := Players[i].Integers[0];
      MSI_Players[i].BreakLoads := Players[i].Integers[1];
      MSI_Players[i].TotalTime := Players[i].Integers[2];
      MSI_Players[i].BreakTime := Players[i].Integers[3];
      MSI_Players[i].Member := Players[i].Booleans[0];
      Players[i].Member := MSI_Players[i].Member;

      if (MSI_Players[i].Active) then
        MSI_Players[i].ReportInfo.FalseReason := '';
    end;
  except
    MSI_Debug('MSI_ResetRemote: ' + ExceptionToString(ExceptionType, ExceptionParam));
  end;
end;

(*
MSI_MirrorTIA
~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_MirrorTIA(TIA: TIntegerArray): TIntegerArray;

Mirrors the integer array (reverses the order).

.. note::

  | Author: Coh3n
  | Last Updated: 15 November 2011 by Coh3n

Example:

.. code-block:: pascal

  reverseArea := MSI_MirrorTSA(SPSAreas);
*)
function MSI_MirrorTIA(TIA: TIntegerArray): TIntegerArray;
var
  i, h: integer;
begin
  h := 0;
  setLength(result, length(TIA));

  for i := high(TIA) downto 0 do
  begin
    result[h] := TIA[i];
    inc(h);
  end;

  setLength(result, h);
end;

(*
MSI_MirrorTSA
~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_MirrorTSA(TSA: TPointArray): TStringArray;

Mirrors the string array (reverses the order).

.. note::

  | Author: Coh3n
  | Last Updated: 12 October 2011 by Coh3n

Example:

.. code-block:: pascal

  reverseArea := MSI_MirrorTSA(SPSAreas);
*)
function MSI_MirrorTSA(TSA: TStringArray): TStringArray;
var
  i, h: integer;
begin
  h := 0;
  setLength(result, length(TSA));

  for i := high(TSA) downto 0 do
  begin
    result[h] := TSA[i];
    inc(h);
  end;

  setLength(result, h);
end;

(*
MSI_MirrorATSA
~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_MirrorATSA(ATSA: array of TStringArray): array of TStringArray;

Mirrors the ATSA (including the strings in the TSAs. Used for reversing MSI's
path's SPS areas as they are 2 dimentional arrays.

.. note::

  | Author: Coh3n
  | Last Updated: 12 October 2011 by Coh3n

Example:

.. code-block:: pascal

  reverseAreas := MSI_MirrorATSA(SPSAreas);
*)
function MSI_MirrorATSA(ATSA: array of TStringArray): array of TStringArray;
var
  i, c: Integer;
begin
  setLength(result, length(ATSA));

  for i := high(ATSA) downto 0 do
  begin
    result[c] := MSI_MirrorTSA(ATSA[i]);
    inc(c);
  end;

  setLength(result, c);
  MSI_SubDebug('MSI_MirrorATSA: New string array ' + toStr(result));
end;

(*
MSI_MirrorTPA
~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_MirrorTPA(TPA: TPointArray): TPointArray;

Mirrors the TPA (reverses the order). Used primarily for reverting the
reflection walking paths so one path can be used to walk to and from locations.

.. note::

  | Author: Sir. R. Magician
  | Last Updated: 28 July 2010 by Sir. R. Magician

Example:

.. code-block:: pascal

  reversePath := MSI_MirrorTPA(path);
*)
function MSI_MirrorTPA(TPA: TPointArray): TPointArray;
var
  i, h: integer;
begin
  h := 0;
  setLength(result, length(TPA));

  for i := high(TPA) downto 0 do
  begin
    result[h] := TPA[i];
    inc(h);
  end;

  setLength(result, h);
end;

(*
MSI_MirrorATPA
~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_MirrorATPA(ATPA: T2DPointArray): T2DPointArray;

Mirrors the ATPA (including the points in the TPAs. Used for reversing MSI's
walking paths as they are 2 dimentional arrays.

.. note::

  | Author: Coh3n
  | Last Updated: 23 June 2011 by Coh3n

Example:

.. code-block:: pascal

  reversePath := MSI_MirrorATPA(path);
*)
function MSI_MirrorATPA(ATPA: T2DPointArray): T2DPointArray;
var
  i, c: Integer;
begin
  setLength(result, length(ATPA));

  for i := high(ATPA) downto 0 do
  begin
    result[c] := MSI_MirrorTPA(ATPA[i]);
    inc(c);
  end;

  setLength(result, c);
  MSI_SubDebug('MSI_MirrorATPA: New ATPA ' + toStr(result));
end;

(*
MSI_BoxToMM
~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_BoxToMM(box: TBox): TBox;

Converts an SPS or tile TBox into a TBox that's on the minimap.  This method is
extremely slow when using SPS so it should be avoided and is used specifically
for debugging.

.. note::

  | Author: Coh3n
  | Last Updated: 27 August 2011 by Coh3n

Example:

.. code-block:: pascal

  var
    loc: TLocation;
  begin
    mmBox := MSI_BoxToMM(loc.subLocs[i].tileBox);
    ...
*)
function MSI_BoxToMM(box: TBox): TBox;
var
  corners: array[0..1] of TPoint;
begin
  if (not MSI_Settings[SETUP_DEBUG_SMART]) then
    exit;

  {$IFDEF SMART}
  {$IFDEF SPS}
  corners[0] := SPS_PosToMM(point(box.x1, box.y1));
  corners[1] := SPS_PosToMM(point(box.x2, box.y2));
  {$ENDIF}

  result := intToBox(corners[0].x, corners[0].y, corners[1].x, corners[1].y);

  if (MSI_Settings[SETUP_DEBUG_SMART]) then
    SMART_DrawBoxEx(true{$IFDEF SIMBAMAJOR990}, false{$ENDIF}, result, clGreen);

  MSI_SubDebug('MSI_BoxToMM(): ' + toStr(result));
  {$ENDIF}
end;

(*
MSI_RandomizeArray
~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_RandomizeArray(arr: TIntegerArray): TIntegerArray;

Returns the array of integers, 'Objs', in a random order.  This is used so the
user can set MSI's Priority Autoing feature so it will look for one object
(i.e. look for one tree) over another.

.. note::

  | Author: NCDS
  | Last Updated: 20 September 2010 by Coh3n

Example:

.. code-block:: pascal

  MSI_FindObject(MSI_GetObject(TREE_NORMAL), moLeft);
*)
function MSI_RandomizeArray(arr: TIntegerArray): TIntegerArray;
var
  i, o: integer;
begin
  setLength(result, length(arr));

  while (length(arr) <> 0) do
    for i := 0 to high(result) do
    begin
      o := random(length(arr));
      result[i] := arr[o];
      deleteValueInIntArray(arr, o);
    end;
end;

(*
MSI_ChooseInteger
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_ChooseInteger(priorities: TIntegerArray): integer;

Chooses an index from 'Priorities' based on the index's value.  The higher the
index value, the higher chance it has of being chosen.

.. note::

  | Author: Sir R. Magician
  | Last Updated: 20 September 2010 by NCDS

Example:

.. code-block:: pascal

  objs := [TREE_NORMAL, TREE_OAK, TREE_WILLOW];
  obj := MSI_ChooseInteger([10, 30, 60]);
  MSI_FindObject(objs[obj], ClickLeft);
*)
function MSI_ChooseInteger(priorities: TIntegerArray): integer;
var
  TIA: TIntegerArray;
  i , sum, currSum: integer;
begin
  setLength(TIA, Length(priorities));
  sum := sumIntegerArray(priorities);

  for i := 0 to high(priorities) do
  begin
    currSum := (currSum + priorities[i]);
    TIA[i] := currSum;
  end;

  sum := (random(sum) + 1);

  for result := 0 to high(TIA) do
    if (sum <= TIA[result]) then
      break;
end;

(*
MSI_RearrangeArray
~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_RearrangeArray(arr, priorities: TIntegerArray): TIntegerArray;

Returns the array 'arr' in a rearranged order based on 'priorities'.  Uses a
combination of MSI_RandomizeArray and MSI_ChooseInteger.

.. note::

  | Author: NCDS
  | Last Updated: 20 September 2010 by Coh3n

Example:

.. code-block:: pascal

  objs := MSI_RearrangeArray([TREE_NORMAL, TREE_OAK], [30, 70]);
*)
function MSI_RearrangeArray(arr, priorities: TIntegerArray): TIntegerArray;
var
  i, o: Integer;
begin
  setLength(result, length(arr));

  if (length(arr) <> length(priorities)) or (length(priorities) = 0) then
  begin
    result := MSI_RandomizeArray(arr);
    exit;
  end;

  while (length(arr) > 0) do
    for i := 0 to high(Result) do
    begin
      o := MSI_ChooseInteger(priorities);
      result[i] := arr[o];

      deleteValueInIntArray(arr, o);
      deleteValueInIntArray(priorities, o);
    end;
end;

(*
MSI_MakeBox
~~~~~~~~~~~

.. code-block:: pascal

  function MSI_MakeBox(center: TPoint; w, h: integer): TBox;

Creates a box with center point 'center' and width and height 'w' and 'h'.

.. note::

  | Author: Sir R. Magician
  | Last Updated: 07 August 2010 by Coh3n

Example:

.. code-block:: pascal

  MiddleTPA(TPA, x, y);
  with MSI_Objects[TREE_NORMAL] do
    MSI_MakeBox(Point(x, y), W, H); // W/H are TMSObject attributes
*)
function MSI_MakeBox(center: TPoint; w, h: integer): TBox;
begin
  result := intToBox(center.x - (w / 2),
                     center.y - (h / 2),
                     center.x + (w / 2),
                     center.Y + (h / 2));
end;


