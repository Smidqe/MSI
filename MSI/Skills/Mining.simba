(*
Mining
======

The Mining file holds any functions or procedures that are specific to the
Mining skill.

The source for can be found
`here <https://github.com/SRL/MSI/raw/master/MSI/Skills/Mining.simba>`_.

*)

const
  SIZE_MULTIPLIER = 1.25;
  ROCK_MAX_DIST   = 40;

(*
MSI_SetupMining
~~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_SetupMining(): Boolean;

Returns true if the player has been properly setup for mining.  Only calls
MSI_SetupWoodcutting to avoid at lot of repeated code and for easier updating.
This is the function that is set to the PlayerSetup pointer in the TScript type.

.. note::

  | Author: Coh3n
  | Last Updated: 06 October 2010 by Coh3n

Example:

.. code-block:: pascal

  MSI_Scripts[SCRIPT_VE_MINER].PlayerSetup := @MSI_SetupMining();
*)
function MSI_SetupMining(): Boolean;
begin
  Result := MSI_SetupWoodcutting(); // To avoid unnecessary repeated code
end;

(*
MSI_WaitWhileMining
~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

  procedure MSI_WaitWhileMining(rock: TMSObject);

Waits while the player is mining.  Performs several failsafe checks such as
tracking the colors found on the rock being mined, inventory count, animations,
and timing out.

.. note::

  | Author: NCDS
  | Last Updated: 28 July 2011 by Coh3n

Example:

.. code-block:: pascal

  if (MSI_FindObject(MSI_GetObject(ROCK_IRON), moLeft)) then
    MSI_WaitWhileMining();
*)
procedure MSI_WaitWhileMining(rock: TMSObject);
var
  i, h, cts, oldInvCount, t, d, rockColorCount: Integer;
  arr, TPA: TPointArray;
  ATPA: T2DPointArray;
begin
  if (not loggedIn) then
    exit;

  flag;
  wait(RandomRange(285, 450));

  if (invFull) then
    Exit;

  MSI_AddHeader('MSI_WaitWhileMining');
  cts := GetColorToleranceSpeed;

  t := (getSystemTime + rock.maxTime + randomRange(-1000, 1000));
  rock.searchArea := MSI_MakeBox(point(rock.x, rock.y),
                                 round(rock.w * SIZE_MULTIPLIER),
                                 round(rock.h * SIZE_MULTIPLIER));

  {$IFDEF SMART}
    if (MSI_Settings[SETUP_DEBUG_SMART]) then
      SMART_DrawBoxEx(true, rock.searchArea, clBlue);
  {$ENDIF}

  TPA := TPAFromBox(rock.searchArea);
  sortTPAFrom(TPA, point(MSCX, MSCY));

  if (length(TPA) > 0) then
  begin
    d := distance(TPA[0].x, TPA[0].y, MSCX, MSCY);
    MSI_Debug('Distance from tracking box to player = '+intToStr(d));

    if (d > ROCK_MAX_DIST) then
    begin
      MSI_Debug('Track box too far from player');
      MSI_CloseHeader('MSI_WaitWhileMining: Finished');
      exit;
    end;
  end;

  oldInvCount := invCount;
  MSI_Debug('InvCount: ' + intToStr(oldInvCount));

  setColorToleranceSpeed(2);
  setToleranceSpeed2Modifiers(rock.hue[0], rock.sat[0]);

  setLength(ATPA, length(rock.colors));
  h := high(ATPA);

  for i := 0 to h do
    findColorsTolerance(ATPA[i], rock.colors[i],
                        rock.searchArea.x1, rock.searchArea.y1,
                        rock.searchArea.x2, rock.searchArea.y2, rock.tol);

  arr := mergeATPA(ATPA);
  rockColorCount := length(arr);

  repeat
    MSI_FindRandoms(false);

    for i := 0 to h do
      findColorsTolerance(ATPA[i], rock.colors[i],
                          rock.searchArea.x1, rock.searchArea.y1,
                          rock.searchArea.x2, rock.searchArea.y2, rock.tol);

    arr := mergeATPA(ATPA);

    {$IFDEF SMART}
    if (MSI_Settings[SETUP_DEBUG_SMART]) then
      SMART_DrawDotsEx(false, arr, clYellow);
    {$ENDIF}

    if (length(arr) < rock.accuracy) then
    begin
      MSI_Debug('Length(arr) = ' + IntToStr(Length(arr)));
      MSI_Debug('Colours found < Accuracy');
      Break;
    end;

    if (Length(Arr) < Round(RockColorCount * 0.25)) then
    begin
      MSI_Debug('Rock Gone, not enough points');
      Break;
    end;

    MSI_AntiBan(RandomRange(0, 3000), ANTI_BAN_CHANCE);

    if (OldInvCount < InvCount) then
    begin
      MSI_Debug('OldInvCount < InvCount');
      Break;
    end;

    if FindChatBoxText('manage to mine', 8, clMessage) then
    begin
      MSI_Debug('Found Text');
      Break;
    end;

    Wait(RandomRange(100, 300));

    if (t < GetSystemTime) then
    begin
      MSI_Debug('Took too long');
      break;
    end;

  until((not LoggedIn));

  MSI_CloseHeader('MSI_WaitWhileMining: Finished');

  setColorToleranceSpeed(cts);
  setToleranceSpeed2Modifiers(0.2, 0.2);
end;

(*
MSI_MineRock
~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_MineRock(which: TIntegerArray): boolean;

Mines the rocks in the array 'which'.  'forceFail' is used to purposely make
the function fail if for some reason the pickaxe can't be found or the player
is unable to mine the rock.

.. note::

  | Author: marpis
  | Last Updated: 10 June 2011 by Coh3n

Example:

.. code-block:: pascal

  if (MSI_MineRock([ROCK_IRON, ROCK_SILVER])) then
    MSI_Debug('Mined rock');
*)
function MSI_MineRock(which: TIntegerArray): boolean;
var
  rockObjs: TMSObjectArray;
  rock: TMSObject;
begin
  if (not loggedIn) then
    exit;

  rockObjs := MSI_GetObjects(which);

  if (MSI_FindObjects(rockObjs, moLeft, rock)) then
  begin
    result := true;
    MSI_WaitWhileMining(rock);
  end else
    result := MSI_FindRandoms(false);
end;

(*
MSI_FillCoalBag
~~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_FillCoalBag: Boolean;

Fills the coal bag with coal that's in the inventory.

.. note::

  | Author: Kyle Undefined

Example:

.. code-block:: pascal

   if (MSI_FillCoalBag()) then
    Exit;
*)
function MSI_FillCoalBag : Boolean;
var
  x, y : Integer;
  coalSlots : TIntegerArray;
begin
  if (not loggedIn) then
    exit;

  MSI_AddHeader('MSI_FillCoalBag');

  coalSlots := MSI_FindItemSlots(ORE_COAL);
  if (MSI_FindTool(COAL_BAG, tab_Inv, x, y)) then
  begin
    MouseItem(RandomRange(Low(coalSlots) + 1, High(coalSlots)), mouse_Left);
    Wait(RandomRange(150, 250));
    Mouse(x, y, 5, 5, mouse_Left);
    Result := True;
    Wait(RandomRange(500, 750));
  end;

  MSI_CloseHeader('MSI_FillCoalBag: Finished');
end;

(*
MSI_MineInvFull
~~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_MineInvFull: Boolean;

Mines rocks until the inventory is full.  This is the function that is pointed
to a scripts 'AutoObjects' attribute.

.. note::

  | Author: Coh3n
  | Last Updated: 20 September 2010 by Coh3n

Example:

.. code-block:: pascal

  MSI_Scripts[SCRIPT_VE_MINER].AutoObjects := @MSI_MineInvFull;
*)
function MSI_MineInvFull: Boolean;
begin
  Result := MSI_AutoInvFull(@MSI_MineRock); // MSI_AutoInvFull in Skills.simba
  
  if (MSI_Players[CurrentPlayer].Scripts[CurrentScript].CoalBag) then
    if (MSI_FillCoalBag()) then
    begin
      Result := false;
      Result := MSI_AutoInvFull(@MSI_MineRock);
    end;
end;


